\ STM8eForth : control structures with relative addressing         TG9541-201124
\ ------------------------------------------------------------------------------

#require >Y

: THEN ( -- ) POSTPONE [ HERE OVER - 1- SWAP C! ] ; IMMEDIATE

: >REL ( -- ) HERE 0 C, ;  \ like >MARK for rel. branch

: ELSE ( -- )  POSTPONE [ ( JRA ) $20 C, ] >REL SWAP POSTPONE THEN ; IMMEDIATE

: JREQ ( F:Z -- ) POSTPONE [ $27 C, ] >REL ; IMMEDIATE

: IF ( n -- ) POSTPONE >Y POSTPONE JREQ ; IMMEDIATE

\\ Example

#require >REL

: ]B@IF ( -- ) 2* $7201 + , , ] >REL ;  \ BTJF  a,#bit,rel

: ]@IF  ( -- ) $90CE , , ( LDW Y,a ) ] POSTPONE JREQ ;

: ]C@IF ( -- ) $C6 C, ,  ( LD  A,a ) ] POSTPONE JREQ ;

NVM
VARIABLE vt
: testb  [ vt 1 ]B@IF ."  set" ELSE ."  not set" THEN ;
: testNZ [ vt ]@IF ."  not " THEN ."  zero" ;
RAM

\\ Alternative coding

\ : >RES ( -- ) HERE OVER - 1- SWAP C! ; \ like >RESOLVE
\ : THEN ( -- ) >RES ; IMMEDIATE
\ : AHEAD ( -- ) POSTPONE [ $20 C, ( JRA xx ) ] >REL ; IMMEDIATE
\ : ELSE ( -- ) POSTPONE AHEAD SWAP POSTPONE THEN ; IMMEDIATE
